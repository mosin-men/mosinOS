# console.bjou
#
# Provides a higher-level interface to the UART device.
#
# The UART driver must be initialized and configured before
# console will work.
#
# console::init() should be called to initialize the singleton
# console's state.

module console

import "defs.bjou"
import "uart.bjou"
using import "cstr.bjou"
using import "util.bjou"

_con       : console

proc init() { _con = console.create() }

type console {
    array : int[2]
    read_fn : <() : char>

    proc create() : console {
        c := { console: }
        c.echo_on()

        return c
    }

    proc echo_on(this) {
        this.read_fn = uart::read_echo
    }

    proc echo_off(this) {
        this.read_fn = uart::read
    }

    proc readchar(this) : char {
        c := '\0'
        while not c    { c = this.read_fn() }
        return c
    }

    proc readln(this, dst : char*) {
        c := '\0'
        while true {
            while not (c = this.read_fn()) {}
            
            if c == '\r' {
                @dst = '\n'
                dst += 1
                break
            } else {
                @dst = c
                dst += 1
            }
        }
        @dst = '\0'
    }

    proc write(this, c : char) {
        if c == '\n' {
            uart::write('\r')
        }
        uart::write(c)
    }

    proc write(this, s : char*) {
        while @s {
            this.write(@s)
            s += 1
        }
    }
}

proc readchar() : char {
    return _con.readchar()
}

proc readln(dst : char*) {
    _con.readln(dst)
}

proc write(c : char) {
    _con.write(c)
}

proc write(s : char*) {
    _con.write(s)
}

proc write(i : i32) {
    buff : char[64]
    itoa(i, buff)
    _con.write(buff)
}

proc writehex(u : u32) {
    buff : char[64]
    itoax(u, buff)
    _con.write("0x")
    _con.write(buff)
}

proc writehex(p : void*) {
    buff : char[64]
    itoax(ptr_to_u32(p), buff)
    _con.write("0x")
    _con.write(buff)
}

proc writebin(u : u32) {
    buff : char[33]
    itoab(u, buff)
    _con.write(buff)
}

proc writeln() {
    _con.write("\n")
}

proc writeln(c : char) {
    _con.write(c)
    _con.write('\n')
}

proc writeln(i : i32) {
    buff : char[64]
    itoa(i, buff)
    _con.write(buff)
    _con.write("\n")
}

proc writehexln(u : u32) {
    buff : char[64]
    itoax(u, buff)
    _con.write("0x")
    _con.write(buff)
    _con.write("\n")
}

proc writehexln(p : void*) {
    buff : char[64]
    itoax(ptr_to_u32(p), buff)
    _con.write("0x")
    _con.write(buff)
    _con.write("\n")
}

proc writebinln(u : u32) {
    buff : char[33]
    itoab(u, buff)
    _con.write(buff)
    _con.write("\n")
}

proc writeln(s : char*) {
    _con.write(s)
    _con.write("\n")
}

proc echo_on() {
    _con.echo_on()
}

proc echo_off() {
    _con.echo_off()
}
