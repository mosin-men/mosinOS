# console.bjou
#
# Provides a higher-level interface to the UART device.
#
# The UART driver must be initialized and configured before
# console will work.
#
# console::init() should be called to initialize the singleton
# console's state.

module console

import "defs.bjou"
import "uart.bjou"
using import "cstr.bjou"
using import "util.bjou"
using import "error.bjou"
using import "none.bjou"
using import "defs.bjou"
using import "ansi_escapes.bjou"

const NL_STR := "\r\n"

_con       : console

proc init() : (error | none) {
    _con = console.create()
    return { none: }
}

type console {
    read_fn : <() : char>

    proc create() : console {
        c := { console: }
        c.echo_on()

        return c
    }

    proc echo_on(this) {
        this.read_fn = uart::read_echo
    }

    proc echo_off(this) {
        this.read_fn = uart::read
    }

    proc readchar(this) : char {
        c := '\0'
        while not c    { c = this.read_fn() }
        return c
    }

    proc readln(this, dst : char*) {
        ptr := dst

        c := '\0'
        while true {
            while not (c = this.read_fn()) {}
            
            if c == '\r' {
                @dst = '\n'
                dst += 1
                break
            } else if c == 127 {
                if dst != ptr {
                    dst -= 1
                    @dst = '\0'
                    write(CURSOR_LEFT)
                    write(CLEAR_LINE)
                }
            } else {
                @dst = c
                dst += 1
            }
        }
        @dst = '\0'
    }

    proc write(this, c : char) {
        if c == '\n' {
            uart::write('\r')
        }
        uart::write(c)
    }

    proc write(this, s : char*) {
        while @s {
            this.write(@s)
            s += 1
        }
    }
}

proc readchar() : char {
    return _con.readchar()
}

proc readln(dst : char*) {
    _con.readln(dst)
}

proc write(c : char) {
    _con.write(c)
}

proc write(s : char*) {
    _con.write(s)
}

proc writepad(s : char*, l : u32) {
    len := cstrlen(s)
    _con.write(s)
    if len < l {
        for i := 0; i < l - len; i += 1 {
            _con.write(' ')
        }
    }
}

proc write(i : i32) {
    buff : char[64]
    itoa(i, buff)
    _con.write(buff)
}

proc writehex(u : u32) {
    buff : char[64]
    itoax(u, buff)
    _con.write("0x")
    _con.write(buff)
}

proc writehex(p : void*) {
    buff : char[64]
    itoax(ptr_to_u32(p), buff)
    _con.write("0x")
    _con.write(buff)
}

proc writebin(u : u32) {
    buff : char[33]
    itoab(u, buff)
    _con.write(buff)
}

proc writeln() {
    _con.write(NL_STR)
}

proc writeln(c : char) {
    _con.write(c)
    _con.write(NL_STR)
}

proc writeln(i : i32) {
    buff : char[64]
    itoa(i, buff)
    _con.write(buff)
    _con.write(NL_STR)
}

proc writehexln(u : u32) {
    buff : char[64]
    itoax(u, buff)
    _con.write("0x")
    _con.write(buff)
    _con.write(NL_STR)
}

proc writehexln(p : void*) {
    buff : char[64]
    itoax(ptr_to_u32(p), buff)
    _con.write("0x")
    _con.write(buff)
    _con.write(NL_STR)
}

proc writebinln(u : u32) {
    buff : char[33]
    itoab(u, buff)
    _con.write(buff)
    _con.write(NL_STR)
}

proc writeln(s : char*) {
    _con.write(s)
    _con.write(NL_STR)
}

proc echo_on() {
    _con.echo_on()
}

proc echo_off() {
    _con.echo_off()
}
